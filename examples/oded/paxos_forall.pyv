# this file is like paxos_epr.pyv, but modified to have a universally
# quantified invariant by adding two ghost functions:
# decision_quorum and choosable_node

sort node
sort value
sort quorum
sort round

immutable constant none: round
immutable relation le(round, round)

# le is a total order
axiom le(X,X)
axiom le(X, Y) & le(Y, Z) -> le(X, Z)
axiom le(X, Y) & le(Y, X) -> X = Y
axiom le(X, Y) | le(Y, X)
axiom le(none,X) # added only to make none defined in initial states

immutable relation member(node, quorum)
axiom forall Q1, Q2. exists N. member(N, Q1) & member(N, Q2)

mutable relation one_a(round)
mutable relation one_b(node, round)
mutable relation left_round(node, round)
mutable relation proposal(round, value)
mutable relation vote(node, round, value)
mutable relation decision(round, value)

# the ghost functions
mutable function decision_quorum(round, value): quorum
mutable function choosable_node(round, value, quorum): node


init !one_a(R)
init !one_b(N, R)
init !left_round(N, R)
init !proposal(R, V)
init !vote(N, R, V)
init !decision(R, V)
init decision_quorum(R1, V1) = decision_quorum(R2, V2)  # added only to make it defined in initial states
init choosable_node(R1, V1, Q1) = choosable_node(R2, V2, Q2)  # added only to make it defined in initial states


transition send_1a(r: round)
  modifies one_a
  r != none &
  (forall R. one_a(R) <-> old(one_a(R)) | R = r)


transition join_round(n: node, r: round)
  modifies one_b, left_round, choosable_node
  r != none &
  old(one_a(r)) &
  !old(left_round(n, r)) &
  (forall N, R. one_b(N, R) <-> old(one_b(N,R)) | N = n & R = r) &
  (forall N, R. left_round(N, R) <-> old(left_round(N, R)) | N = n & !le(r, R)) &
  (forall R,V,Q.  (member(n,Q) & !le(r,R) & !old(vote(n,R,V))) -> choosable_node(R,V,Q) = n ) &
  (forall R,V,Q. !(member(n,Q) & !le(r,R) & !old(vote(n,R,V))) -> choosable_node(R,V,Q) = old(choosable_node(R,V,Q)) )


transition propose(r: round, q: quorum, maxr: round, v: value)
  modifies proposal
  r != none &
  (forall V. !old(proposal(r, V))) &
  (forall N. member(N, q) -> old(one_b(N, r))) &
  ((maxr = none & forall N,MAXR,V. !(member(N, q) & !le(r,MAXR) & old(vote(N,MAXR,V)))) |
   (maxr != none &
    (exists N. member(N, q) & !le(r,maxr) & old(vote(N,maxr,v))) &
    (forall N,MAXR,V. (member(N, q) & !le(r,MAXR) & old(vote(N,MAXR,V))) -> le(MAXR,maxr)))) &
  (forall R, V. proposal(R, V) <-> old(proposal(R, V)) | (R = r & V = v))


transition cast_vote(n: node, v: value, r: round)
  modifies vote
  r != none &
  !old(left_round(n, r)) &
  old(proposal(r, v)) &
  (forall N, R, V. vote(N, R, V) <-> old(vote(N, R, V)) | N = n & R = r & V = v)


transition decide(r: round, v: value, q: quorum)
  modifies decision, decision_quorum
  r != none &
  (forall N. member(N, q) -> old(vote(N, r, v))) &
  (forall R, V. decision(R, V) <-> old(decision(R, V)) | R = r & V = v) &
  (forall R, V. !(R = r & V = v) -> decision_quorum(R, V) = old(decision_quorum(R, V))) &
  decision_quorum(r, v) = q


# safety property
safety decision(R1, V1) & decision(R2, V2) -> V1 = V2

# proposals are unique per round
invariant proposal(R,V1) & proposal(R,V2) -> V1 = V2

# only vote for proposed values
invariant vote(N,R,V) -> proposal(R,V)

# decisions come from quorums of votes
invariant decision(R, V) & member(N, Q) & Q = decision_quorum(R, V) -> vote(N, R, V)

# properties of none
invariant !vote(N,none,V)

# properties of one_b, left_round
invariant one_b(N,R2) & !le(R2,R1) -> left_round(N,R1)

# choosable_node is a witness
invariant member(N1,Q) & left_round(N1,R) & !vote(N1,R,V) & N2 = choosable_node(R,V,Q) ->
    member(N2,Q) & left_round(N2,R) & !vote(N2,R,V)

# choosable invariant
invariant forall R1:round, R2:round, V1:value, V2:value, Q:quorum. !le(R2,R1) & proposal(R2,V2) & V1 != V2 & N = choosable_node(R1,V1,Q)->
    member(N,Q) & left_round(N,R1) & !vote(N,R1,V1)

sat trace {
  assert init
  send_1a
  assert exists R. one_a(R)
  join_round
  propose
  cast_vote
  decide
  assert exists R,V. decision(R,V)
}

sat trace {
  assert init
  send_1a
  assert exists R. one_a(R)
  join_round
  propose
  cast_vote
  decide
  assert exists R,V. decision(R,V)
  send_1a
  join_round
  propose
  cast_vote
  decide
  assert exists R1, R2, V. R1 != R2 & decision(R1,V) & decision(R2,V)
}
