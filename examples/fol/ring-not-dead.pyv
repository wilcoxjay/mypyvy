sort node

immutable relation le(node, node) @no_minimize

axiom le(X,X)
axiom le(X, Y) & le(Y, Z) -> le(X, Z)
axiom le(X, Y) & le(Y, X) -> X = Y
axiom le(X, Y) | le(Y, X)

immutable relation btw(node, node, node) @no_minimize

axiom btw(W,X,Y) & btw(W,Y,Z) -> btw(W,X,Z)
axiom btw(W,X,Y) -> !btw(W,Y,X)
axiom btw(W,X,Y) | btw(W,Y,X) | W=X | W=Y | X=Y
axiom btw(X,Y,Z) -> btw(Y,Z,X)
axiom exists N. forall X. le(X, N) # there is a node with maximal id

mutable relation leader(node)
mutable relation pending(node, node)
mutable relation sent(node)

init !leader(N)
init !pending(N1, N2)
init !sent(N)

transition send(n: node, next: node)
  modifies pending, sent
  (forall Z. n != next & ((Z != n & Z != next) -> btw(n,next,Z))) &
  (forall N1, N2. pending(N1, N2) <-> old(pending(N1, N2)) | N1 = n & N2 = next) &
  (forall N. sent(N) <-> (old(sent(N)) | N = n))

transition recv(sender: node, n: node, next: node)
  modifies leader, pending
  (forall Z. n != next & ((Z != n & Z != next) -> btw(n,next,Z))) &
  old(pending(sender, n)) &
  (sender = n ->
    (forall N. leader(N) <-> old(leader(N)) | N = n) &
    (forall N1, N2.
      !(N1 = sender & N2 = n) ->  # message may or may not be removed
      (pending(N1, N2) <-> old(pending(N1, N2))))) &
  (sender != n ->
    (forall N. leader(N) <-> old(leader(N))) &
    (le(n, sender) ->
      (forall N1, N2.
        !(N1 = sender & N2 = n) ->  # message may or may not be removed
        (pending(N1, N2) <-> old(pending(N1, N2)) | N1 = sender & N2 = next))) &
    (!le(n, sender) ->
      (forall N1, N2.
        !(N1 = sender & N2 = n) ->  # message may or may not be removed
        (pending(N1, N2) <-> old(pending(N1, N2))))))

safety [one_leader] leader(L1) & leader(L2) -> L1 = L2
safety [not_dead] (exists L. leader(L)) | (exists M,N. pending(M, N)) | (exists N. !sent(N))

invariant [leader_max] leader(L) -> le(N, L)
invariant pending(S, D) & btw(S, N, D) -> le(N, S)
invariant pending(L, L) -> le(N, L)

invariant [for_not_dead] exists N. (forall X. le(X,N)) & (!sent(N) | leader(N) | (exists M. pending(N, M)))