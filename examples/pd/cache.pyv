sort address
sort value
sort core

mutable function memory(address) : value

mutable function cache(core, address) : value

mutable relation modified(core, address)
mutable relation exclusive(core, address)
mutable relation shared(core, address)
mutable relation invalid(core, address)

mutable relation proc_read(core, address)
mutable relation proc_write(core, address, value)

mutable relation bus_in_use()

mutable relation bus_read(core, address)
mutable relation bus_read_for_ownership(core, address)
mutable relation bus_upgrade(core, address)

mutable relation bus_transfer(value)

init !modified(C, A)
init !exclusive(C, A)
init !shared(C, A)
init invalid(C, A)
init !proc_read(C, A)
init !proc_write(C, A, V)
init !bus_in_use
init !bus_read(C, A)
init !bus_read_for_ownership(C, A)
init !bus_upgrade(C, A)
init !bus_transfer(V)

# just to make everything defined in initial states
# TODO: find a better solution, as this also affects verification
init memory(X1) = memory(X2)
init cache(X1, Y1) = cache(X2, Y2)

transition issue_proc_read_invalid(c: core, a: address)
  modifies bus_in_use, proc_read, bus_read
  old(invalid(c, a)) &
  old(!bus_in_use) &
  bus_in_use &
  (forall C, A. proc_read(C, A) <-> old(proc_read(C, A)) | C = c & A = a) &
  (forall C, A. bus_read(C, A) <-> old(bus_read(C,A)) | C != c & A = a)

transition do_bus_read_invalid(c: core, a: address)
  modifies bus_read
  old(bus_read(c, a)) &
  old(invalid(c, a)) &
  (forall C, A. bus_read(C, A) <-> old(bus_read(C, A)) & !(C = c & A = a))

transition do_bus_read_valid(c: core, a: address, v: value)
  modifies bus_read, modified, exclusive, shared, bus_transfer, memory
  old(bus_read(c, a)) &
  old(!invalid(c, a)) &
  old(cache(c, a)) = v &
  (forall C, A. bus_read(C, A) <-> old(bus_read(C, A)) & !(C = c & A = a)) &
  (forall C, A. shared(C, A) <-> old(shared(C, A)) | C = c & A = a) &
  (forall C, A. modified(C, A) <-> old(modified(C, A)) & !(C = c & A = a)) &
  (forall C, A. exclusive(C, A) <-> old(exclusive(C, A)) & !(C = c & A = a)) &
  (old(modified(c, a)) ->  # write back
    (forall A.
       (A != a ->
         (memory(A) = old(memory(A))))) &
     (memory(a) = v)) &
  (!old(modified(c, a)) ->
    (forall A. memory(A) = old(memory(A)))) &
  (forall V. bus_transfer(V) <-> old(bus_transfer(V)) | V = v)

transition complete_proc_read_invalid_shared(c: core, a: address, v: value)
  modifies invalid, shared, bus_transfer, cache, bus_in_use, proc_read
  old(invalid(c, a)) &
  old(proc_read(c, a)) &
  old(bus_transfer(v)) &
  (forall C, A. !old(bus_read(C, A))) &
  (forall V. !bus_transfer(V)) &
  (forall C, A. invalid(C, A) <-> old(invalid(C, A)) & !(C = c & A = a)) &
  (forall C, A. shared(C, A) <-> old(shared(C, A)) | C = c & A = a) &
  (forall C, A.
     !(C = c & A = a) ->
     (cache(C, A) = old(cache(C, A)))) &
  cache(c, a) = v &
  old(bus_in_use) &  # added just to make it defined; it's an invariant that bus_in_use is true here
  !bus_in_use &
  (forall C, A. proc_read(C, A) <-> old(proc_read(C, A)) & !(C = c & A = a))

transition complete_proc_read_invalid_exclusive(c: core, a: address, v: value)
  modifies invalid, exclusive, cache, bus_in_use, proc_read
  old(invalid(c, a)) &
  old(proc_read(c, a)) &
  (forall V. !old(bus_transfer(V))) &
  (forall C, A. !old(bus_read(C, A))) &
  old(memory(a) = v) &

  (forall C, A. invalid(C, A) <-> old(invalid(C, A)) & !(C = c & A = a)) &
  (forall C, A. exclusive(C, A) <-> old(exclusive(C, A)) | C = c & A = a) &
  (forall C, A.
     !(C = c & A = a) ->
     (cache(C, A) = old(cache(C, A)))) &
  cache(c, a) = v &
  old(bus_in_use) &  # added just to make it defined; it's an invariant that bus_in_use is true here
  !bus_in_use &
  (forall C, A. proc_read(C, A) <-> old(proc_read(C, A)) & !(C = c & A = a))

transition issue_proc_write_invalid(c: core, a: address, v: value)
  modifies bus_in_use, proc_write, bus_read_for_ownership
  old(invalid(c, a)) &
  old(!bus_in_use) &
  bus_in_use &
  (forall C, A, V. proc_write(C, A, V) <-> old(proc_write(C, A, V)) | C = c & A = a & V = v) &
  (forall C, A. bus_read_for_ownership(C, A) <-> old(bus_read_for_ownership(C,A)) | C != c & A = a)

transition do_bus_read_for_ownership_invalid(c: core, a: address)
  modifies bus_read_for_ownership
  old(bus_read_for_ownership(c, a)) &
  old(invalid(c, a)) &
  (forall C, A. bus_read_for_ownership(C, A) <-> old(bus_read_for_ownership(C, A)) & !(C = c & A = a))

transition do_bus_read_for_ownership_valid(c: core, a: address, v: value)
  modifies bus_read_for_ownership, modified, exclusive, shared, invalid, bus_transfer, memory
  old(bus_read_for_ownership(c, a)) &
  old(!invalid(c, a)) &
  old(cache(c, a)) = v &
  (forall C, A. bus_read_for_ownership(C, A) <-> old(bus_read_for_ownership(C, A)) & !(C = c & A = a)) &
  (forall C, A. invalid(C, A) <-> old(invalid(C, A)) | C = c & A = a) &
  (forall C, A. shared(C, A) <-> old(shared(C, A)) & !(C = c & A = a)) &
  (forall C, A. modified(C, A) <-> old(modified(C, A)) & !(C = c & A = a)) &
  (forall C, A. exclusive(C, A) <-> old(exclusive(C, A)) & !(C = c & A = a)) &
  (old(modified(c, a)) ->  # write back
    (forall A.
       (A != a ->
         (memory(A) = old(memory(A))))) &
     (memory(a) = v)) &
  (!old(modified(c, a)) ->
    (forall A. memory(A) = old(memory(A)))) &
  (forall V. bus_transfer(V) <-> old(bus_transfer(V)) | V = v)

transition complete_proc_write_invalid(c: core, a: address, v: value)
  modifies invalid, modified, bus_transfer, cache, bus_in_use, proc_write
  old(invalid(c, a)) &
  old(proc_write(c, a, v)) &
  (forall C, A. !old(bus_read_for_ownership(C, A))) &
  (forall V. !bus_transfer(V)) &
  (forall C, A. invalid(C, A) <-> old(invalid(C, A)) & !(C = c & A = a)) &
  (forall C, A. modified(C, A) <-> old(modified(C, A)) | C = c & A = a) &
  (forall C, A.
     !(C = c & A = a) ->
     (cache(C, A) = old(cache(C, A)))) &
  cache(c, a) = v &
  old(bus_in_use) &  # added just to make it defined; it's an invariant that bus_in_use is true here
  !bus_in_use &
  (forall C, A, V. proc_write(C, A, V) <-> old(proc_write(C, A, V)) & !(C = c & A = a & V = v))

transition proc_write_exclusive(c: core, a: address, v: value)
  modifies exclusive, modified, cache
  old(exclusive(c, a)) &
  !old(bus_in_use) &
  (forall C, A. exclusive(C, A) <-> old(exclusive(C, A)) & !(C = c & A = a)) &
  (forall C, A. modified(C, A) <-> old(modified(C, A)) | C = c & A = a) &
  (forall C, A.
     !(C = c & A = a) ->
     (cache(C, A) = old(cache(C, A)))) &
  cache(c, a) = v

transition issue_proc_write_shared(c: core, a: address, v: value)
  modifies bus_in_use, proc_write, bus_upgrade
  old(shared(c, a)) &
  !old(bus_in_use) &
  bus_in_use &
  (forall C, A, V. proc_write(C, A, V) <-> old(proc_write(C, A, V)) | C = c & A = a & V = v) &
  (forall C, A. bus_upgrade(C, A) <-> old(bus_upgrade(C,A)) | C != c & A = a)

transition do_bus_upgrade(c: core, a: address)
  modifies bus_upgrade, shared, invalid
  old(bus_upgrade(c, a)) &
  (forall C, A. bus_upgrade(C, A) <-> old(bus_upgrade(C, A)) & !(C = c & A = a)) &
  (forall C, A. invalid(C, A) <-> old(invalid(C, A)) | C = c & A = a) &
  (forall C, A. shared(C, A) <-> old(shared(C, A)) & !(C = c & A = a))

transition complete_proc_write_shared(c: core, a: address, v: value)
  modifies bus_in_use, proc_write, cache, shared, modified
  old(shared(c, a)) &
  old(proc_write(c, a, v)) &
  (forall C, A. !old(bus_upgrade(C, A))) &
  (forall C, A. shared(C, A) <-> old(shared(C, A)) & !(C = c & A = a)) &
  (forall C, A. modified(C, A) <-> old(modified(C, A)) | C = c & A = a) &
  (forall C, A.
     !(C = c & A = a) ->
     (cache(C, A) = old(cache(C, A)))) &
  cache(c, a) = v &
  (forall C, A, V. proc_write(C, A, V) <-> old(proc_write(C, A, V)) & !(C = c & A = a & V = v)) &
  old(bus_in_use) &  # added just to make it defined; it's an invariant that bus_in_use is true here
  !bus_in_use


transition evict_modified(c: core, a: address)
  modifies memory, modified, invalid
  !old(bus_in_use) &
  old(modified(c, a)) &
  (forall A.
    (A != a ->
     (memory(A) = old(memory(A))))) &
  memory(a) = old(cache(c, a)) &  # write back
  (forall C, A. modified(C, A) <-> old(modified(C, A)) & !(C = c & A = a)) &
  (forall C, A. invalid(C, A) <-> old(invalid(C, A)) | C = c & A = a)

transition evict_exclusive_or_shared(c: core, a: address)
  modifies exclusive, shared, invalid
  !old(bus_in_use) &
  old(exclusive(c, a) | shared(c, a)) &
  (forall C, A. exclusive(C, A) <-> old(exclusive(C, A)) & !(C = c & A = a)) &
  (forall C, A. shared(C, A) <-> old(shared(C, A)) & !(C = c & A = a)) &
  (forall C, A. invalid(C, A) <-> old(invalid(C, A)) | C = c & A = a)

safety !invalid(C, A) & !modified(C, A) -> cache(C, A) = memory(A)

# each cache line is in at most one state
invariant !(invalid(C, A) & modified(C, A))
invariant !(invalid(C, A) & exclusive(C, A))
invariant !(invalid(C, A) & shared(C, A))
invariant !(modified(C, A) & exclusive(C, A))
invariant !(modified(C, A) & shared(C, A))
invariant !(exclusive(C, A) & shared(C, A))

invariant exclusive(C1, A) & C1 != C2 -> invalid(C2, A)
invariant modified(C1, A) & C1 != C2 -> invalid(C2, A)

invariant bus_read(C, A) -> bus_in_use
invariant bus_read_for_ownership(C, A) -> bus_in_use
invariant bus_upgrade(C, A) -> bus_in_use
invariant bus_transfer(V) -> bus_in_use

invariant proc_read(C, A) -> bus_in_use
invariant proc_write(C, A, V) -> bus_in_use

invariant proc_write(C1, A, V) & bus_read_for_ownership(C2, A) -> invalid(C1, A)
invariant proc_write(C1, A, V) & bus_upgrade(C2, A) -> shared(C1, A)

invariant !(proc_write(C, A, V) & bus_upgrade(C, A))

invariant proc_write(C, A, V1) & shared(C, A) -> !bus_transfer(V2)

invariant proc_read(C1, A1) & proc_read(C2, A2) -> C1 = C2 & A1 = A2
invariant proc_write(C1, A1, V1) & proc_write(C2, A2, V2) -> C1 = C2 & A1 = A2 & V1 = V2
invariant !(proc_read(C1, A1) & proc_write(C2, A2, V))

invariant !(proc_read(C1, A1) & bus_read_for_ownership(C2, A2))
invariant !(proc_read(C1, A1) & bus_upgrade(C2, A2))
invariant !(proc_write(C1, A1, V) & bus_read(C2, A2))

invariant proc_read(C1, A1) & bus_read(C2, A2) -> A1 = A2
invariant proc_write(C1, A1, V) & bus_read_for_ownership(C2, A2) -> A1 = A2
invariant proc_write(C1, A1, V) & bus_upgrade(C2, A2) -> A1 = A2

invariant proc_read(C1, A) & !bus_read(C2, A) & C1 != C2 -> shared(C2, A) | invalid(C2, A)
invariant proc_read(C1, A) & !bus_read(C2, A) & C1 != C2 & shared(C2, A) -> bus_transfer(cache(C2, A))

invariant proc_read(C, A) & bus_transfer(V) -> memory(A) = V
invariant proc_read(C1, A) & bus_transfer(V) & !invalid(C2, A) -> cache(C2, A) = V

invariant proc_write(C1, A, V) & invalid(C1, A) & !bus_read_for_ownership(C2, A) & C1 != C2 -> invalid(C2, A)

invariant proc_write(C1, A, V) & shared(C1, A) & !bus_upgrade(C2, A) & C1 != C2 -> invalid(C2, A)

invariant !(bus_transfer(V) & modified(C1, A) & (proc_read(C2, A) | proc_write(C2, A, V2)))

invariant !(bus_upgrade(C1, A) & exclusive(C2, A))
invariant !(bus_upgrade(C1, A) & modified(C2, A))
