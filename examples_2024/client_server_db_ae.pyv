# A simple example of a system where a forall-exists invariant is
# required, adapted from client_server_db_ae.ivy from the code
# examples of:
#
# Yotam M. Y. Feldman, Oded Padon, Neil Immerman, Mooly Sagiv, and
# Sharon Shoham. Bounded Quantifier Instantiation for Checking
# Inductive Invariants. TACAS 2017, LMCS 2019.
# https://doi.org/10.1007/978-3-662-54577-5_5
# https://doi.org/10.23638/LMCS-15(3:18)2019

sort db_request_id
sort node
sort response
sort request

immutable relation match(request, response)

mutable relation request_sent(node, request)
mutable relation response_sent(node, response)
mutable relation response_received(node, response)

mutable relation db_request_sent(db_request_id, request)
mutable relation db_response_sent(db_request_id, response)

mutable relation t(db_request_id, node)

init !request_sent(N,R)
init !response_sent(N,P)
init !response_received(N,P)
init !db_request_sent(I, R)
init !db_response_sent(I,P)
init !t(I,N)

transition new_request(n:node, r:request)
  modifies request_sent
  & forall N,R. new(request_sent(N,R)) <-> (request_sent(N,R) | (N = n & R = r))

transition server_process_request(n:node, r:request, i:db_request_id)
  modifies t, db_request_sent
  & request_sent(n,r)
  & (forall N. !t(i,N))
  & (forall I,N. new(t(I,N)) <-> (t(I,N) | (I = i & N = n)))
  & (forall I,R. new(db_request_sent(I,R)) <-> (db_request_sent(I,R) | (I = i & R = r)))

transition db_process_request(i:db_request_id, r:request, p:response)
  modifies db_response_sent
  & db_request_sent(i,r)
  & match(r,p)
  & (forall I,P. new(db_response_sent(I,P)) <-> (db_response_sent(I,P) | (I = i & P = p)))

transition server_process_db_response(n:node, i:db_request_id, p:response)
  modifies response_sent
  & db_response_sent(i,p)
  & t(i,n)
  & (forall N,P. new(response_sent(N,P)) <-> (response_sent(N,P) | (N = n & P = p)))

transition receive_response(n:node, p:response)
  modifies response_received
  & response_sent(n,p)
  & (forall N,P. new(response_received(N,P)) <-> (response_received(N,P) | (N = n & P = p)))

safety forall N,P. response_received(N,P) -> exists R. request_sent(N,R) & match(R,P)
invariant forall N,P. response_sent(N,P) -> exists R. request_sent(N,R) & match(R,P)
invariant forall I,R. db_request_sent(I,R) -> exists N. t(I,N) & request_sent(N,R)
invariant forall I,P. db_response_sent(I,P) -> exists R. db_request_sent(I,R) & match(R,P)
invariant t(I,N1) & t(I,N2) -> N1 = N2
